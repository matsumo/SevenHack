buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

// Library version
def versionSupportLibrary = '25.0.1'

// App version
def versionMajor = 0
def versionMinor = 9
def versionPatch = 1

def packageName = 'io.github.waka.sevenhack'
def testInstrumentationRunnerPackageName = 'android.support.test.runner.AndroidJUnitRunner'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion 21
        targetSdkVersion 25
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner testInstrumentationRunnerPackageName
    }

    def secretFile = file("${rootDir}/secret/secret.gradle")
    if (secretFile.exists()) {
        apply from: secretFile.absolutePath
        signingConfigs {
            debug {
                storeFile rootProject.file("secret/debug.jks")
                storePassword project.ext.debugStorePassword
                keyAlias project.ext.debugKeyAlias
                keyPassword project.ext.debugKeyPassword
            }
            release {
                storeFile rootProject.file("secret/release.jks")
                storePassword project.ext.releaseStorePassword
                keyAlias project.ext.releaseKeyAlias
                keyPassword project.ext.releaseKeyPassword
            }
        }
    } else {
        signingConfigs {
            debug {
                storeFile rootProject.file("secret/debug.jks")
                storePassword project.ext.debugStorePassword
                keyAlias project.ext.debugKeyAlias
                keyPassword project.ext.debugKeyPassword
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        develop {
            applicationId "${packageName}.develop"
        }
        production {
            applicationId packageName
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        return true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
        return true
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:${versionSupportLibrary}"
    compile "com.android.support:support-v4:${versionSupportLibrary}"
    compile "com.android.support:design:${versionSupportLibrary}"
    compile "com.android.support:cardview-v7:${versionSupportLibrary}"
    compile "com.android.support:recyclerview-v7:${versionSupportLibrary}"
    compile "com.android.support:palette-v7:${versionSupportLibrary}"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    // dependency injection
    compile 'com.google.dagger:dagger:2.7'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.7'
    provided 'javax.annotation:jsr250-api:1.0'

    // rx
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // date
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'

    // image
    compile "com.squareup.picasso:picasso:2.5.2"

    // api
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile ('com.squareup.retrofit2:converter-simplexml:2.1.0'){
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // data
    compile 'com.squareup.sqlbrite:sqlbrite:1.1.1'
    compile "org.parceler:parceler-api:1.1.6"
    annotationProcessor "org.parceler:parceler:1.1.6"

    // development
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile 'com.facebook.stetho:stetho:1.4.2'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    // testing
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
